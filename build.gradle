buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath group: 'org.xerial', name: 'sqlite-jdbc', version: '3.30.1'
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.4-M3'
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.3.70'
    id 'nu.studer.jooq' version '4.1'
    id "org.flywaydb.flyway" version "6.4.1"
    id 'com.github.johnrengelman.shadow' version '6.0.0'
    id "de.undercouch.download" version "4.1.1"
}

apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.shadow'

mainClassName = 'nec.gui.NecApp'

group 'org.example'
version '1.0-SNAPSHOT'

repositories {
    maven { url 'https://dl.bintray.com/kotlin/kotlin-eap' }
    mavenCentral()
    jcenter()
    maven { url "https://dl.bintray.com/ekito/koin" }
    maven { url 'https://jitpack.io' }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "org.jetbrains.kotlin:kotlin-reflect"

    // https://mvnrepository.com/artifact/no.tornado/tornadofx
    compile group: 'no.tornado', name: 'tornadofx', version: '1.7.20'

    // https://mvnrepository.com/artifact/io.projectreactor/reactor-core
    compile group: 'io.projectreactor', name: 'reactor-core', version: '3.3.7.RELEASE'
    compile "com.github.shadskii:reactorfx:2.0.0"

    implementation group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.7.30'
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.30'

    // https://mvnrepository.com/artifact/org.jetbrains.kotlinx/kotlinx-serialization-runtime
    implementation group: 'org.jetbrains.kotlinx', name: 'kotlinx-serialization-runtime', version: '0.20.0-1.4-M3'

    // db
    implementation group: 'org.xerial', name: 'sqlite-jdbc', version: '3.30.1'
    jooqRuntime group: 'org.xerial', name: 'sqlite-jdbc', version: '3.30.1'
    implementation 'org.jooq:jooq:3.13.1'
    implementation 'org.flywaydb:flyway-core:6.4.1'

    // https://mvnrepository.com/artifact/org.jgrapht/jgrapht-core
    compile group: 'org.jgrapht', name: 'jgrapht-core', version: '1.4.0'

    // https://mvnrepository.com/artifact/net.java.dev.jna/jna-platform
    compile group: 'net.java.dev.jna', name: 'jna-platform', version: '5.6.0'
    // https://mvnrepository.com/artifact/net.java.dev.jna/jna
    compile group: 'net.java.dev.jna', name: 'jna', version: '5.6.0'

    // https://mvnrepository.com/artifact/com.google.protobuf/protobuf-java
    compile group: 'com.google.protobuf', name: 'protobuf-java', version: '3.12.2'

    compile files('libs/ortools-java-7.7.7810.jar')
    compile files('libs/ortools-linux-x86-64-7.7.7810.jar')
    compile files('libs/ortools-darwin-7.7.7810.jar')
    compile files('libs/ortools-win32-x86-64-7.7.7810.jar')
}

def orToolsDest = './libs'
task downloadOrTools(type: Download) {
    src([
            'https://github.com/google/or-tools/releases/download/v7.7/java_win.zip',
            'https://github.com/google/or-tools/releases/download/v7.7/java_osx.zip',
            'https://github.com/google/or-tools/releases/download/v7.7/java_linux.tar.gz'
    ])
    dest orToolsDest
    overwrite false
}
task unpackOrToolsWin(dependsOn: downloadOrTools, type: Copy) {
    from zipTree("${downloadOrTools.dest}/java_win.zip")
    exclude "**/*-sources.jar", "**/*-javadoc.jar"
    into downloadOrTools.dest
    eachFile {
        path = name
    }
    includeEmptyDirs = false
}
task unpackOrToolsOsx(dependsOn: downloadOrTools, type: Copy) {
    from zipTree("${downloadOrTools.dest}/java_osx.zip")
    exclude "**/*-sources.jar", "**/*-javadoc.jar"
    into downloadOrTools.dest
    eachFile {
        path = name
    }
    includeEmptyDirs = false
}
task unpackOrToolsLinux(dependsOn: downloadOrTools, type: Copy) {
    from tarTree(resources.gzip("${downloadOrTools.dest}/java_linux.tar.gz"))
    exclude "*-sources.jar", "**/*-javadoc.jar"
    into downloadOrTools.dest
}
task unpackOrTools(dependsOn: [downloadOrTools, unpackOrToolsWin, unpackOrToolsOsx, unpackOrToolsLinux])

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

flyway {
    url = 'jdbc:sqlite:./nec.db'
}

jooq {
    version = '3.13.1'
    nec(sourceSets.main) {
        jdbc {
            driver = 'org.sqlite.JDBC'
            url = 'jdbc:sqlite:./nec.db'
        }
        generator {
            name = 'org.jooq.codegen.JavaGenerator'
            database {
                forcedTypes {
                    forcedType {
                        name = 'BOOLEAN'
                        includeTypes = '(?i:INTEGER\\(1\\))'
                    }
                }
            }
            strategy {
                name = 'org.jooq.codegen.DefaultGeneratorStrategy'
            }
            database {
                name = 'org.jooq.meta.sqlite.SQLiteDatabase'
            }
            generate {
                relations = true
                deprecated = false
                records = true
                pojos = true
                fluentSetters = true
            }
            target {
                packageName = 'nec.dbmodel'
            }
        }
    }
}

generateNecJooqSchemaSource.dependsOn flywayMigrate
compileKotlin.dependsOn generateNecJooqSchemaSource, unpackOrTools
